# Model of plant electron transport in vivo with realistic boundary conditions

# The MIT License (MIT)
# Copyright (c) 2016 Alejandro Morales Sierra
# Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
# The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.



# 5.2. Photosystem II

## 5.2.1. Energy transfer

### Eqn 5.1 is Implicitly generated by ODEDSL
#species PSII{Antenna[1]} in leaf = 0 in umol/m^2 # C_2^*


# Excitation of chlorophylls in LHCII assuming at most 1 exciton per PSII unit and taking into account
# 1. Connectivity among antennae (kUU)
# 2. Absorption by auxilliary pigments and non-photosynthetic pigments (alphap)
# 3. Chloroplast movement (alphar)
# 4. Balance between PSI and PSII (sigma2)
variable vUU = PSII{Antenna[1]}*kUU/PSII in 1/s
reaction vin2 in leaf = PSII{Antenna[0]}*(PAR*alphap*sigma2*alphar/PSII + vUU) for 1 PSII{Antenna[0]} -> 1 PSII{Antenna[1]} in umol/m^2/s
parameter kUU = 5e9 in 1/s

# Deexcitation of chlorophylls taking into account
# 1. Connectivity among antennae (kUU)
# 2. Chlorophyll fluorescence (kf2)
# 3. Basal rate of heat dissipation (kD2) and enhanced by qE mechanism (Q*kE)
reaction vout2 in leaf = PSII{Antenna[1]}*(kf2 + kUU + kD2 + Q*kE) for 1 PSII{Antenna[1]} -> 1 PSII{Antenna[0]} in umol/m^2/s
parameter kf2 = 6.67e7 in 1/s # k_f2
parameter kD2 = 2.33e8 in 1/s # k_D2
parameter kE = 1.9e9 in 1/s # 2e9. k_E

# Fluorescerence yield
observed Fluor = PSII{Antenna[1]}*kf2/PAR

### Eqn 5.2


### Eqn 5.3
variable alpha = Ct/(Ct + KChl) # alpha
parameter KChl = 76 in umol/m^2 # K_Chl

variable alphap = alpha*(fblue*cfblue + fgreen*cfgreen + fred)
parameter fblue = 0.33
parameter fgreen = 0.33
parameter fred = 0.33
parameter cfblue = 0.76
parameter cfgreen = 0.89

### Eqn 5.4
variable Ct = PSII*aII + PSI*aI in umol/m^2 # C_t
#variable aII = aI*PSI*sigma2/(PSII*(1 - sigma2))
#parameter sigma2 = 0.55
parameter aII = 200
parameter aI = 210 # a_I
variable sigma2 = PSII*aII/Ct

# Steady-state relative absorbance due to chloroplast movement
variable Ib = PAR*fblue in umol/m^2/s
variable alpharss = min(1.0 + Ib/Iac*alpharac, 1.0 + alpharac - (alphar_alpha*(Ib - Iac)  +
                    alpharav -sqrt((alphar_alpha*(Ib - Iac) + alpharav)^2 -
                    4*alphar_alpha*thetaalphar*alpharav*(Ib - Iac)))/(2*thetaalphar))

# Initial slope of the light response curve of chloroplast movement
variable alphar_alpha = alphar_alpha25*exp(-(T - Tref)*DHaAlphar/(Tref*R*T)) in 1/umol*m^2*s
constant Tref = 298.15 in K
parameter alphar_alpha25 = 6.55e-3 in 1/umol*m^2*s
parameter DHaAlphar = 67.32 in kJ/mol # We add the negative sign in the equation so this should remain positive
parameter DHaKalpha = 58.03 in kJ/mol
parameter Iac = 1.6 in umol/m^2/s
parameter alpharac = 0.05
parameter alpharav = 0.25
parameter thetaalphar = 0.36
parameter Kialpha25 = 1.49e-3 in 1/s
parameter Kdalpha25 = 1.86e-3 in 1/s

# Dynamics of the relative absorbance due to chloroplast movement
derivative d_alphar_dt of alphar = ifelse(alpharss > alphar, (alpharss - alphar)*Kialpha, (alpharss - alphar)*Kdalpha) in 1/s
state alphar = 1

# Rate constant at which absorbance increases due to chloroplast movement
variable Kialpha = Kialpha25*exp((T - Tref)*DHaKalpha/(Tref*R*T)) in 1/s

# Rate constant at which absorbance decreases due to chloroplast movement
variable Kdalpha = Kdalpha25*exp((T - Tref)*DHaKalpha/(Tref*R*T)) in 1/s

## Dynamic changes in PsbS protonation
derivative d_fPsbSp_dt of fPsbSp = ifelse(fPsbSpss > fPsbSp, (fPsbSpss - fPsbSp)*kPsbSi, (fPsbSpss - fPsbSp)*kPsbSd) in 1/s # dPsbS_r/dt
state fPsbSp = 0 #
parameter kPsbSi = 0.017 in 1/s # k_qEPi
parameter kPsbSd = 0.03 in 1/s # k_qEPd

## Steady-state protonation of PsbS for wt levels
variable fPsbSpss = 1/(1.0 + 10.0^(np*(pHl - pKp))) # PsbS_rss
parameter np = 1.0 # n_p
parameter pKp = 6.3 # 6.3. pK_p

## Relative changes in the pool of xanthophylls
reaction v_AX_VX in lumen = AX*kepo1 - VX*kvde1*fpH for 1 AX -> 1 VX in 1/s
reaction v_AX_ZX in lumen = AX*kvde2*fpH - ZX*kepo2 for 1 AX -> 1 ZX in 1/s
parameter kepo1 = 1.83e-4 in 1/s # k_epo1
parameter kepo2 = 4.83e-4 in 1/s # k_epo2
parameter kvde1 = 2.33e-3 in 1/s # k_VDE1
parameter kvde2 = 8.33e-3 in 1/s # k_VDE2
species AX in lumen = 0.0
species VX in lumen = 1.0
species ZX in lumen = 0.0

## pH dependence of VDE
variable fpH = 1/(1.0 + 10.0^(nv*(pHl - pKv))) # f_pH
parameter nv = 3 # n_v
parameter pKv = 6.4 # pK_v


## Effect of xanthophylls and PsbS on NPQ: 4 state model
## This is based on the model by Matuszynska and Ebenhoh (2016)
# State 0 is ignored sa it is parameterized independently
observed Q = gamma1*(1 - ZX)*fPsbSp + gamma2*ZX*fPsbSp + gamma3*ZX*(1 - fPsbSp)
parameter gamma1 = 0.3
parameter gamma2 = 0.6
parameter gamma3 = 0.1

# Photoinhibition
#reaction vqI in leaf = PSII{Antenna[1] P680[3]}*kDinh for 1 PSII{Antenna[1] P680[3]} -> 1 PSII{Antenna[0] P680[3]} in umol/m^2/s
#parameter kDinh = 1e9 in 1/s

## 5.2.2. Electron transport within PSII

### Eqn 5.11 is implicitly generated by ODEDSL
species PSII in leaf = 1.105 in umol/m^2 #1.13 in umol/m^2 #
component Antenna ["0", "1"] of PSII = [1, 0]
component Tyr ["0", "p"] of PSII = [1, 0]
#component P680 ["e", "p", "0", "3"] of PSII = [0, 0 , 1, 0]
component P680 ["e", "p", "0"] of PSII = [0, 0 , 1]
component Pheo ["0", "1"] of PSII = [1, 0]
component Qa [ "0" , "1" ] of PSII = [ 1 , 0 ]
component Qb [ "0" , "1" , "2", "e"] of PSII = [ 0 , 0, 0, 1]

### 5.12 is not part of the model (explanation of syntax)

### Eqn. 5.13
variable fpHoec = 1/(1 + 10^(nOEC*(pKOEC - pHl)))
parameter nOEC = 1.06753 # n_oec
parameter pKOEC = 5.28168  # pK_oec


### Eqn 5.14
reaction voec in leaf = PSII{Tyr[p]}*koec*fpHoec for 1 PSII{Tyr[p]} -> 1 PSII{Tyr[0]} + 1 Hlt in umol/m^2/s
parameter koec = 10892.48 in 1/s #

### Eqn. 5.16 & 5.17
reaction vtp in leaf = PSII{Tyr[0] P680[p]}*ktp - PSII{Tyr[p] P680[0]}*ktp/exp((-Gtp - ftp*DPsi*F)/(R*T))  for 1 PSII{Tyr[0] P680[p]} -> 1 PSII{Tyr[p] P680[0]} in umol/m^2/s
parameter ktp = 6.3e6 in 1/s
parameter ftp = 0.1
parameter Gtp = -2.81 in kJ/mol

### Eqn 5.18
reaction vac in leaf = PSII{Antenna[1] P680[0]}*kac for 1 PSII{Antenna[1] P680[0]} -> 1 PSII{Antenna[0] P680[e]} in umol/m^2/s
parameter kac = 1.5e10 in 1/s # k_ac

### Eqn. 5.19
reaction vca in leaf = PSII{Antenna[0] P680[e]}*kca for 1 PSII{Antenna[0] P680[e]} -> 1 PSII{Antenna[1] P680[0]} in umol/m^2/s
parameter kca = 3e10 in 1/s # k_ca

### Eqn 5.20
reaction vcso in leaf = PSII{P680[e] Pheo[0] Qa[0]}*kcso for 1 PSII{P680[e] Pheo[0] Qa[0]} -> 1 PSII{P680[p] Pheo[1] Qa[0]} in umol/m^2/s
parameter kcso = 3e9 in 1/s # k_cso

### Eqn 5.21
reaction vcsc in leaf = PSII{P680[e] Pheo[0] Qa[1]}*kcsc for 1 PSII{P680[e] Pheo[0] Qa[1]} -> 1 PSII{P680[p] Pheo[1] Qa[1]} in umol/m^2/s
parameter kcsc = 4.7e8 in 1/s # k_csc

### Eqn 5.22
reaction vcro in leaf = PSII{P680[p] Pheo[1] Qa[0]}*kcso/exp((-Gcso - fcs*DPsi*F)/(R*T)) for  1 PSII{P680[p] Pheo[1] Qa[0]} -> 1 PSII{P680[e] Pheo[0] Qa[0]} in umol/m^2/s
parameter Gcso = -5.7 in kJ/mol # -11 to reduce charge recombination
parameter fcs = 0.4

### Eqn 5.23
reaction vcrc in leaf = PSII{P680[p] Pheo[1] Qa[1]}*kcsc/exp((-Gcsc - fcs*DPsi*F)/(R*T)) for 1 PSII{P680[p] Pheo[1] Qa[1]} -> 1 PSII{P680[e] Pheo[0] Qa[1]} in umol/m^2/s
parameter Gcsc = -0.8 in kJ/mol

# Make sure Qa is oxidised in darkness
reaction vQaox in leaf = ifelse(PAR < oneumolm2s, 1, 0)*PSII{Qa[1]}*kox for 1 PSII{Qa[1]} -> 1 PSII{Qa[0]} in umol/m^2/s
constant oneumolm2s = 1 in umol/m^2/s
parameter kox = 10 in 1/s

### Formation of 3P680 (the steps afterwards are not simulated explicitly). A 3P680 indicates a damaged D1
#reaction v3P680 in leaf = min(PSII{P680[p] Pheo[1]}*k3P680, PAR*phiPDmax) for 1 PSII{P680[p] Pheo[1]} -> 1 PSII{P680[3] Pheo[0]} in umol/m^2/s
#parameter k3P680 = 0 in 1/s
#parameter phiPDmax = 1e-7

#observed PSIId = PSII{P680[3]}/PSII
#observed phiPD = v3P680/PAR

### Rate of repair
#reaction vrep in leaf = PSII{P680[3]}*krep for 1 PSII{P680[3]} -> 1 PSII{P680[0]} in umol/m^2/s
#parameter krep = 1.92e-4 in 1/s

### Eqn 5.24
reaction vpa in leaf = PSII{Pheo[1] Qa[0]}*kpa for 1 PSII{Pheo[1] Qa[0]} -> 1 PSII{Pheo[0] Qa[1]} in umol/m^2/s
parameter kpa = 2.3e9 in 1/s # k_pa

### Eqn 5.25
reaction vap in leaf = PSII{Pheo[0] Qa[1]}*kpa/exp((EmQa - EmPheo -fpa*DPsi)*F/(R*T)) for 1 PSII{Pheo[0] Qa[1]} -> 1 PSII{Pheo[1] Qa[0]} in umol/m^2/s
parameter EmQa = -80 in mV
parameter EmPheo = -610 in mV
parameter fpa = 0.4 # f_pa

### Eqn 5.26
#  Very small probability that this recombination happens...
#reaction vcra in leaf =  PSII{P680[p] Qa[1]}*kcra for 1 PSII{P680[p] Qa[1]} -> 1 PSII{P680[0] Qa[0]} in umol/m^2/s
#parameter kcra = 1e2 in 1/s # k_cra

### Eqn 5.27
reaction vab1 in leaf = PSII{Qa[1] Qb[0]}*kab1  for 1 PSII{Qa[1]Qb[0]} -> 1 PSII{Qa[0]Qb[1]} in umol/m^2/s
parameter kab1 = 5e3 in 1/s #

### Eqn 5.28
reaction vba1 in leaf = PSII{Qa[0] Qb[1]}*kab1/exp((EmQb - EmQa -fab*DPsi)*F/(R*T)) for  1 PSII{Qa[0]Qb[1]} -> 1 PSII{Qa[1]Qb[0]} in umol/m^2/s
parameter fab = 0.1 #
parameter EmQb = 0 in mV

### Eqn 5.29
reaction vab2 in leaf = PSII{Qa[1] Qb[1]}*kab2  for 1 PSII{Qa[1]Qb[1]} -> 1 PSII{Qa[0]Qb[2]} in umol/m^2/s
parameter kab2 = 2e3 in 1/s #

### Eqn 5.30
reaction vba2 in leaf = PSII{Qa[0] Qb[2]}*kab2/exp((EmQb1 - EmQa -fab*DPsi)*F/(R*T)) for  1 PSII{Qa[0]Qb[2]} -> 1 PSII{Qa[1]Qb[1]} in umol/m^2/s
parameter EmQb1 = 160 in mV

### Eqn 5.31
reaction vPQH2bind in leaf = PSII{Qb[e]}*PQH2f*kbPQH2b for 1 PSII{Qb[e]} + 1 PQH2 -> 1 PSII{Qb[2]} + 2 Hst in umol/m^2/s # ->
parameter kbPQH2b = 500 in 1/s #
observed PQH2f = PQH2/(PQ + PQH2)

### Eqn 5.32
reaction vPQH2release in leaf = PSII{Qb[2]}*kbPQH2r for 1 PSII{Qb[2]} + 2 Hst -> 1 PSII{Qb[e]} + 1 PQH2 in umol/m^2/s # ->
parameter kbPQH2r = 500 in 1/s #

### Eqn 5.33
reaction vPQbind in leaf = PSII{Qb[e]}*PQf*kbPQb for 1 PSII{Qb[e]} + 1 PQ -> 1 PSII{Qb[0]} in umol/m^2/s
parameter kbPQb = 500 in 1/s #
variable PQf = PQ/(PQ + PQH2)

### Eqn 5.34
reaction vPQrelease in leaf = PSII{Qb[0]}*kbPQr for 1 PSII{Qb[0]} -> 1 PSII{Qb[e]} + 1 PQ in umol/m^2/s
parameter kbPQr = 500 in 1/s




# PTOX
reaction vPTOX in leaf = PQH2*kPTOX for 1 PQH2 -> 1 PQ in umol/m^2/s
parameter kPTOX = 0.1 in 1/s # 3


# 2. Cytochrome b6f

### Eqn 5.35 is implicitly generated by ODEDSL
species cyt in leaf = 0.51 in umol/m^2 #0.56 in umol/m^2
component Qn [0 ,1, 2, "e"] of cyt = [0, 0, 0, 1]
component Qp [0, 2, "e"] of cyt = [0, 0, 1]
component PC [0 ,1, "e"] of cyt = [0, 0, 1]


### Eqn 5.36 - 5.38
reaction vPQH2_ox1 in leaf = cyt_K/(1 + 10^(nc*(pKc - pHl)))*cyt{Qp[2]PC[0]Qn[0]} - cyt{Qp[0]PC[1]Qn[1]}*cyt_K/cyt_KE1 for 1 cyt{Qp[2]PC[0]Qn[0]}  -> 1 cyt{Qp[0]PC[1]Qn[1]} + 2 Hlt in umol/m^2/s
reaction vPQH2_ox2 in leaf = cyt_K/(1 + 10^(nc*(pKc - pHl)))*cyt{Qp[2]PC[0]Qn[1]} - cyt{Qp[0]PC[1]Qn[2]}*cyt_K/cyt_KE2 for 1 cyt{Qp[2]PC[0]Qn[1]}  -> 1 cyt{Qp[0]PC[1]Qn[2]} + 2 Hlt in umol/m^2/s
parameter cyt_K = 500 in 1/s
parameter pKc = 6.1 # 6.52
parameter nc = 1.53


### Eqn 5.39
variable cyt_KE1 = exp((Em_PQ1 + Em_PC_7 - 2*Em_PQH2_7)*F/(R*T))
parameter Em_PQ1 = -67.5 in mV
parameter Em_PC_7 = 365 in mV
parameter Em_PQH2_7 = 80 in mV

### Eqn 5.40
variable cyt_KE2 = exp((Em_PQ2 + Em_PC_7 - 2*Em_PQH2_7)*F/(R*T))
parameter Em_PQ2 = 227.5 in mV

### Eqn 5.41
reaction vpPQH2bind in leaf = cyt{Qp[e]}*PQH2f*kpPQH2b  for 1 cyt{Qp[e]} + 1 PQH2 -> 1 cyt{Qp[2]} in umol/m^2/s
parameter kpPQH2b = 90345 in 1/s

### Eqn 5.42
reaction vpPQH2release in leaf = cyt{Qp[2]}*kpPQH2r  for 1 cyt{Qp[2]} -> 1 cyt{Qp[e]} + 1 PQH2 in umol/m^2/s
parameter kpPQH2r = 3400 in 1/s

### Eqn 5.43
reaction vpPQbind in leaf = cyt{Qp[e]}*PQf*kpPQb for 1 cyt{Qp[e]} + 1 PQ -> 1 cyt{Qp[0]} in umol/m^2/s
parameter kpPQb   = 31320 in 1/s

### Eqn 5.44
reaction vpPQrelease in leaf = cyt{Qp[0]}*kpPQr for 1 cyt{Qp[0]} -> 1 cyt{Qp[e]}  + 1 PQ in umol/m^2/s
parameter kpPQr   = 1600 in 1/s

### Eqn 5.45
reaction vnPQH2bind in leaf = cyt{Qn[e]}*PQH2f*knPQH2b for 1 cyt{Qn[e]} + 1 PQH2 -> 1 cyt{Qn[2]} + 2 Hst in umol/m^2/s # ->
parameter knPQH2b = 2469 in 1/s

### Eqn 5.46
reaction vnPQH2release in leaf = cyt{Qn[2]}*knPQH2r for 1 cyt{Qn[2]} + 2 Hst -> 1 cyt{Qn[e]} + 1 PQH2 in umol/m^2/s # ->
parameter knPQH2r = 2200 in 1/s

### Eqn 5.47
reaction vnPQbind in leaf = cyt{Qn[e]}*PQf*knPQb for 1 cyt{Qn[e]} + 1 PQ -> 1 cyt{Qn[0]} in umol/m^2/s
parameter knPQb   = 277058 in 1/s

### Eqn 5.48
reaction vnPQrelease in leaf = cyt{Qn[0]}*knPQr for 1 cyt{Qn[0]} -> 1 cyt{Qn[e]} + 1 PQ in umol/m^2/s
parameter knPQr   = 2.6 in 1/s

### Eqn 5.49
reaction vcPCrbind in leaf = cyt{PC[e]}*PCf*kcPCrb for 1 cyt{PC[e]} + 1 PCr -> 1 cyt{PC[1]} in umol/m^2/s
parameter kcPCrb = 1e5 in 1/s

### Eqn 5.50
reaction vcPCrrelease in leaf = cyt{PC[1]}*kcPCrr for 1 cyt{PC[1]} -> 1 cyt{PC[e]} + 1 PCr in umol/m^2/s
parameter kcPCrr = 5e3 in 1/s

### Eqn 5.51
reaction vcPCobind in leaf = cyt{PC[e]}*(1 - PCf)*kcPCob for 1 cyt{PC[e]} + 1 PCo -> 1 cyt{PC[0]} in umol/m^2/s
parameter kcPCob = 1e5 in 1/s

### Eqn 5.52
reaction vcPCorelease in leaf = cyt{PC[0]}*kcPCor for 1 cyt{PC[0]} -> 1 cyt{PC[e]} + 1 PCo  in umol/m^2/s
parameter kcPCor = 5e3 in 1/s


# 3. Photosystem I

### Eqn 5.53 is generated by ODEDSL
species PSI in leaf = 0.85 in umol/m^2 #1.27 in umol/m^2 # Schlotter 2014
component PCd ["0" , "1", "e"] of PSI = [0 , 0, 1]
component P700 ["0", "e", "p"] of PSI = [1, 0, 0]
component A0 ["0", "1"] of PSI = [1, 0]
component Fd ["0" , "1", "e"] of PSI = [0, 0, 1]

### Eqn 5.54
reaction vac1 in leaf = PSI{P700[0]}*PAR*alphap*alphar*(1 - sigma2)*Phi1/PSI for 1 PSI{P700[0]} -> 1 PSI{P700[e]} in umol/m^2/s
parameter Phi1 = 1

### Eqn 5.55
reaction vcs1 in leaf = PSI{P700[e] A0[0]}*kcs1 for 1  PSI{P700[e] A0[0]} -> 1  PSI{P700[p] A0[1]} in umol/m^2/s
parameter kcs1 = 1e12 in 1/s

### Eqn 5.56
reaction vcr1 in leaf = PSI{P700[p] A0[1]}*kcr1 for 1  PSI{P700[p] A0[1]} -> 1  PSI{P700[0] A0[0]} in umol/m^2/s
parameter kcr1 = 1e2 in 1/s

### Eqn 5.57 - 5.59
reaction vFdr in leaf = PSI{A0[1] Fd[0]}*kFdr - PSI{A0[0] Fd[1]}*kFdr/exp(F*(Em_Fd - Em_A0)/(R*T)) for 1  PSI{A0[1] Fd[0]} -> 1  PSI{A0[0] Fd[1]} in umol/m^2/s
variable Em_Fd = Em_Fd_7 - V_per_pH*(pHs - 7) in mV
parameter kFdr = 1e6 in 1/s
parameter Em_A0 = -1.1e3 in mV
parameter Em_Fd_7 = -420 in mV

### Eqn. 5.60
reaction v_P700r in leaf = PSI{PCd[1]P700[p]}*kP700r for 1 PSI{PCd[1] P700[p]} -> 1 PSI{PCd[0] P700[0]} in umol/m^2/s
parameter kP700r = 5.8e4 in 1/s #

### Eqn. 5.61
reaction v_PCr in leaf = PSI{PCd[0] P700[0]}*kPCr for 1 PSI{PCd[0] P700[0]} -> 1 PSI{PCd[1] P700[p]} in umol/m^2/s
parameter kPCr = 4.5e3 in 1/s #


### Eqn. 5.62
reaction vpPCrbind in leaf = PSI{PCd[e]}*PCf*kpPCrb for 1 PSI{PCd[e]} + 1 PCr -> 1 PSI{PCd[1]} in umol/m^2/s
parameter kpPCrb = 2.3e5 in 1/s

### Eqn. 5.63
reaction vpPCobind in leaf = PSI{PCd[e]}*(1 - PCf)*kpPCob for 1 PSI{PCd[e]} + 1 PCo -> 1 PSI{PCd[0]} in umol/m^2/s
parameter kpPCob = 1.05e5 in 1/s

### Eqn. 5.64
reaction vpPCrrelease in leaf = PSI{PCd[1]}*kpPCrr for 1 PSI{PCd[1]} -> 1 PSI{PCd[e]} + 1 PCr in umol/m^2/s
parameter kpPCrr = 2.8e3 in 1/s

### Eqn. 5.65
reaction vpPCorelease in leaf = PSI{PCd[0]}*kpPCor for 1 PSI{PCd[0]} -> 1 PSI{PCd[e]} + 1 PCo in umol/m^2/s
parameter kpPCor = 7.35e3 in 1/s

# Binding and release when P700 is reduced
### Eqn. 5.62
#reaction vpPCrrbind in leaf = PSI{PCd[e] P700[0]}*PCf*kpPCrrb for 1 PSI{PCd[e] P700[0]} + 1 PCr -> 1 PSI{PCd[1] P700[0]} in umol/m^2/s
#parameter kpPCrrb = 5.6e5 in 1/s

### Eqn. 5.63
#reaction vpPCorbind in leaf = PSI{PCd[e] P700[0]}*(1 - PCf)*kpPCorb for 1 PSI{PCd[e] P700[0]} + 1 PCo -> 1 PSI{PCd[0] P700[0]} in umol/m^2/s
#parameter kpPCorb = 2.7e5 in 1/s

### Eqn. 5.64
#reaction vpPCrrrelease in leaf = PSI{PCd[1] P700[0]}*kpPCrrr for 1 PSI{PCd[1] P700[0]} -> 1 PSI{PCd[e] P700[0]} + 1 PCr in umol/m^2/s
#parameter kpPCrrr = 2.4e3 in 1/s

### Eqn. 5.65
#reaction vpPCorrelease in leaf = PSI{PCd[0] P700[0]}*kpPCorr for 1 PSI{PCd[0] P700[0]} -> 1 PSI{PCd[e] P700[0]} + 1 PCo in umol/m^2/s
#parameter kpPCorr = 6.7e3 in 1/s

# Binding and release when P700 is oxidised

### Eqn. 5.62
#reaction vpPCrobind in leaf = PSI{PCd[e] P700[p]}*PCf*kpPCrob for 1 PSI{PCd[e] P700[p]} + 1 PCr -> 1 PSI{PCd[1] P700[p]} in umol/m^2/s
#parameter kpPCrob = 2.7e5 in 1/s

### Eqn. 5.63
#reaction vpPCoobind in leaf = PSI{PCd[e] P700[p]}*(1 - PCf)*kpPCoob for 1 PSI{PCd[e] P700[p]} + 1 PCo -> 1 PSI{PCd[0] P700[p]} in umol/m^2/s
#parameter kpPCoob = 2.4e5 in 1/s

### Eqn. 5.64
#reaction vpPCrorelease in leaf = PSI{PCd[1] P700[p]}*kpPCror for 1 PSI{PCd[1] P700[p]} -> 1 PSI{PCd[e] P700[p]} + 1 PCr in umol/m^2/s
#parameter kpPCror = 2.4e3 in 1/s

### Eqn. 5.65
#reaction vpPCoorelease in leaf = PSI{PCd[0] P700[p]}*kpPCoor for 1 PSI{PCd[0] P700[p]} -> 1 PSI{PCd[e] P700[p]} + 1 PCo in umol/m^2/s
#parameter kpPCoor = 11.0e3 in 1/s

### Eqn. 5.66
reaction vFdrbind in leaf = PSI{Fd[e]}*Fdf*kFdrb for 1 PSI{Fd[e]} + 1 Fdr -> 1 PSI{Fd[1]} in umol/m^2/s
parameter kFdrb = 1e4 in 1/s

### Eqn. 5.67
reaction vFdobind in leaf = PSI{Fd[e]}*(1 - Fdf)*kFdob for 1 PSI{Fd[e]} + 1 Fdo -> 1 PSI{Fd[0]} in umol/m^2/s
parameter kFdob = 1e4 in 1/s

### Eqn. 5.68
reaction vFdrrelease in leaf = PSI{Fd[1]}*kFdrr for 1 PSI{Fd[1]} -> 1 PSI{Fd[e]} + 1 Fdr in umol/m^2/s
parameter kFdrr = 1e4 in 1/s

### Eqn. 5.69
reaction vFdorelease in leaf = PSI{Fd[0]}*kFdor for 1 PSI{Fd[0]} -> 1 PSI{Fd[e]} + 1 Fdo in umol/m^2/s
parameter kFdor = 1e4 in 1/s


### Eqn. 5.70
#reaction vFNR in leaf = FNR_K*(Fdf*NADP - (1 - Fdf)*NADPH/FNR_KE)/(FNR_Km_NADP*(1 + NADP/FNR_Km_NADP + NADPH/FNR_Km_NADPH))  for 2 Fdr + 1 NADP + 1 Hst -> 2 Fdo + 1 NADPH  in umol/m^2/s
reaction vFNR in leaf = FNR_K*Fdr*NADP/((Fdr + FNR_Km_Fdr*stroma)*(NADP + FNR_Km_NADP)) -
                        FNR_K*Fdo*NADPH/(FNR_KE*(Fdo + FNR_Km_Fdo*stroma)*(NADPH + FNR_Km_NADPH))  for 2 Fdr + 1 NADP + 1 Hst -> 2 Fdo + 1 NADPH  in umol/m^2/s

parameter FNR_K = 1600 in umol/m^2/s
parameter FNR_Km_NADPH = 35 in uM
parameter FNR_Km_NADP  = 50 in uM
parameter FNR_Km_Fdr = 10 in uM
parameter FNR_Km_Fdo  = 1 in uM

### Eqn. 5.71
variable FNR_KE = exp(2*F*(Em_NADPH - Em_Fd)/(R*T))
variable Em_NADPH = Em_NADPH_7 - V_per_pH*(pHs - 7) in mV
parameter Em_NADPH_7 = -300 in mV # Strand et al. (2015)

### Eqn. 5.72 - 5.101 are generated by ODEDSL
species PQH2 in leaf = 0 in umol/m^2
species PQ in leaf = 6.63 in umol/m^2 # 7.62. Assume 6 times the amount of PSII
species PCr in leaf = 0 in umol/m^2
species PCo in leaf = 2.34 in umol/m^2 # 2.72 Schlotter 2014
species Fdr in leaf = 0 in umol/m^2
species Fdo in leaf = 1.20 in umol/m^2 # 1.41
species Thr in stroma = 0 in mM
species Tho in stroma = 0.1 in mM






### Eqn. 102
#reaction vFTR in leaf = kFdThR*(Fdr*Tho - Fdo*Thr/KEFTR) for 2 Fdr + 1 Tho + 2 Hst -> 2Fdo + 1 Thr in umol/m^2/s # ->
#parameter kFdThR = 100 in 1/mM/s
reaction vFTR in stroma = FTR_Vmax*(Tho*Fdr/((Tho + FTR_Km_Tho)*(Fdr + FTR_Km_Fdr*stroma)) - Thr*Fdo/(KEFTR*(Thr + FTR_Km_Thr)*(Fdo + FTR_Km_Fdo*stroma)))
                          for 2 Fdr + 1 Tho + 2 Hst -> 2Fdo + 1 Thr in mM/s
parameter FTR_Vmax = 0.1 in mM/s
parameter FTR_Km_Tho = 3 in uM
parameter FTR_Km_Thr = 3 in uM
parameter FTR_Km_Fdr = 20 in uM
parameter FTR_Km_Fdo = 20 in uM

### Eqn. 103
variable KEFTR = exp(2*F*(Em_Th - Em_Fd)/(R*T))
variable Em_Th = Em_Th_7 - V_per_pH*(pHs - 7) in mV
parameter Em_Th_7 = -290 in mV


### Eqn. 104 & 105 are generated by ODEDSL

### Eqn. 106
observed LEFII = voec in umol/m^2/s

### Eqn. 107
observed LEFcyt = vPQH2_ox1 + vPQH2_ox2 in umol/m^2/s







### Eqn. 108
reaction vFFP in leaf = kFPf*Pi*ATPase_0 - ATPase_Pi*kFPr for 1 ATPase_0 -> 1 ATPase_Pi in umol/m^2/s # 1 Pi
parameter kFPr = 2000 in 1/s
parameter kFPf = 810 in 1/mM/s

### Eqn. 109
reaction vFFD in leaf = kFDf*ADP*ATPase_0 - ATPase_ADP*kFDr for 1 ATPase_0 + 1 ADP -> 1 ATPase_ADP in umol/m^2/s
parameter kFDf = 12361 in 1/mM/s
parameter kFDr = 368 in 1/s

### Eqn. 110
reaction vFPFDP in leaf = kFDf*ADP*ATPase_Pi - ATPase_ADP_Pi*kFDr for 1 ATPase_Pi + 1 ADP -> 1 ATPase_ADP_Pi in umol/m^2/s

### Eqn. 111
reaction vFDFDP in leaf = kFPf*Pi*ATPase_ADP - ATPase_ADP_Pi*kFPr for 1 ATPase_ADP -> 1 ATPase_ADP_Pi in umol/m^2/s # 1 Pi

### Eqn. 112
reaction vFFT in leaf = kFTf*ATP*ATPase_0 - ATPase_ATP*kFTr for 1 ATPase_0 + 1 ATP -> 1 ATPase_ATP in umol/m^2/s
parameter kFTr = 219 in 1/s
parameter kFTf = 2144 in 1/mM/s

### Eqn. 113
reaction vFDPFT in leaf = ATPase_ADP_Pi*kF1 - ATPase_ATP*kFT2 for 1 ATPase_ADP_Pi -> 1 ATPase_ATP in umol/m^2/s # Consumption and production of H+ is parameterized (see derivatives below)
derivative d_Hlt_dt of Hlt = -HPR*vFDPFT/lumen in umol/m^2/s
derivative d_Hst_dt of Hst = HPR*vFDPFT/stroma -(2.0 + 2.0*phi)*Vr/(2 + 1.5*phi) in umol/m^2/s
parameter HPR = 4.67
observed vH = HPR*vFDPFT in umol/m^2/s

### Eqn. 114
variable kF1 = kF10*p1 in 1/s
parameter kF10 = 5130.0 in 1/s

### Eqn. 115
variable kFT2 = kFT20*p2 in 1/s
parameter kFT20 = 2160.0 in 1/s

### Eqn. 116
variable p1 = KFc/(1.0 + KFc)*x^4.0/D
parameter KFc = 3.1

### Eqn. 117
variable p2 = 1.0/(1.0 + KFc)/D

### Eqn. 118
variable D = 1.0 + x + x^2.0 + x^3.0 + x^4.0

### Eqn. 119
variable x = (fFr*10.0^(pmf/V_per_pH) + (1 - fFr)*10.0^((pmf - pmfd)/V_per_pH))/KF
variable fFr = ATPaser/(ATPaser + ATPaseo)
parameter KF = 110
constant V_per_pH = 59 in mV
parameter pmfd = 60 in mV

### Eqn. 120 - 124 are generated by ODEDSL
species ATPase_0 in leaf = 0.55 in umol/m^2 #0.63 in umol/m^2
species ATPase_ADP in leaf = 0 in umol/m^2
species ATPase_Pi in leaf = 0 in umol/m^2
species ATPase_ADP_Pi in leaf = 0 in umol/m^2
species ATPase_ATP in leaf = 0 in umol/m^2

### Eqn. 125
reaction vFr in stroma = kFr/stroma*(Thr*ATPaseo - Tho*ATPaser/KE_ThATPase) for 1 Thr + 1 ATPaseo -> 1 Tho + 1 ATPaser in mM/s
parameter kFr = 100 in 1/mM/s # 20(Tho + Thr) = 2

### Eqn. 126
variable KE_ThATPase = exp(2*F*(Em_ATPase - Em_Th)/(R*T))
variable Em_ATPase = Em_ATPase_7 - V_per_pH*(pHs - 7) in mV
parameter Em_ATPase_7 = -290 in mV

### Eqn. 127 is generated by ODEDSL
species ATPaser in leaf = 0 in umol/m^2
species ATPaseo in leaf = 0.63 in umol/m^2





### Eqn. 128
derivative d_pHl_dt of pHl = -d_Hlt_dt*Buffer_lumen in 1/s
parameter Buffer_lumen = 33.3 in dm^3/mol
state pHl = 7.8
species Hlt in lumen = 1 in mol/dm^3

derivative d_pHs_dt of pHs = -d_Hst_dt*Buffer_stroma in 1/s
parameter Buffer_stroma = 33.3 in dm^3/mol
state pHs = 7.8
species Hst in stroma = 1 in mol/dm^3


### Eqn. 129 & 130 generated by ODEDSL (as d_Hlt_dt)

### Eqn. 131
#observed pmf = DPsi + (pHs - pHl)*2.3*R*T/F in mV
#parameter pHs = 7.8

### Eqn. 132
# 1 = K
# 2 = Mg
# 3 = Cl
#reaction Fi1 in thylakoid = Perm1*(CiL*fout1 + CiS*(1 - fout1))*DeltaMui1*F/(R*T)   for 1 CiL -> 1 CiS in mol/m^2/s
#reaction Fi2 in thylakoid = Perm2*(CiL2*fout1 + CiS2*(1 - fout1))*DeltaMui2*F/(R*T) for 1 CiL2 -> 1 CiS2 in mol/m^2/s
#reaction Fi3 in thylakoid = Perm3*(CiL3*fout1 + CiS3*(1 - fout1))*DeltaMui3*F/(R*T) for 1 CiL3 -> 1 CiS3 in mol/m^2/s
#parameter Perm1 =  3.6e-8 in cm/s
#parameter Perm2 =  3.6e-8 in cm/s
#parameter Perm3 =  1.8e-8 in cm/s
#variable fout1 = ifelse(DeltaMui1 > zerovoltage, 1, 0)
#variable fout2 = ifelse(DeltaMui1 > zerovoltage, 1, 0)
#variable fout3 = ifelse(DeltaMui1 > zerovoltage, 1, 0)
#constant zerovoltage = 0 in mV

### Eqn. 133
#variable DeltaMui1 = DPsi + 2.3*R*T/F*log10(CiL/CiS) in mV
#variable DeltaMui2 = DPsi + 2.3*R*T/F*log10(CiL2/CiS2) in mV
#variable DeltaMui3 = -DPsi + 2.3*R*T/F*log10(CiL3/CiS3) in mV

### Eqn. 134 and 135 generated by ODEDSL
#species CiL in lumen = 10 in mM
#species CiS in stroma = 10 in mM
#species CiL2 in lumen = 10 in mM
#species CiS2 in stroma = 10 in mM
#species CiL3 in lumen = 10 in mM
#species CiS3 in stroma = 10 in mM

### Eqn. 136
#derivative d_DPsi_dt of DPsi =  (d_Hlt_dt + d_CiL_dt + 2*d_CiL2_dt-+ d_CiL3_dt)*lumen/thylakoid*F/Cmem in mV/s
derivative d_DPsi_dt of DPsi =  d_Hlt_dt*lumen/thylakoid*F/Cmem + v_counter in mV/s
parameter Cmem = 0.6 in uF/cm^2 #

# Alternative model (OK for steady-state, not for dynamics)
state DPsi = 0 in mV
variable dpHC = (pHs - pHl)*2.3*R*T/F in mV
observed pmf = DPsi + dpHC in mV
variable pmfss = min(dpHC*(pmf_a + pmf_b*onemV)/onemV, pmf_a + pmf_b*dpHC) in mV
constant onemV = 1 in mV
parameter pmf_a = 30 in mV # 84
parameter pmf_b = 1.866 # 0.76
variable DPsiSS = pmfss - dpHC in mV
parameter K_PsiSS = 10 in 1/s
variable v_counter = (DPsiSS - DPsi)*K_PsiSS in V/s



### Eqn. 137
reaction v_NDH in leaf = kNDH*Fdr*PQ/((NDH_Km_Fdr + Fdr)*(NDH_Km_PQ + PQ)) - kNDH*Fdo*PQH2/KNDH/((NDH_Km_Fdo + Fdo)*(NDH_Km_PQH2 + PQH2)) for 2 Fdr + 1 PQ + 6 Hst -> 2 Fdo + 1 PQH2 + 4 Hlt in umol/m^2/s #  ->
#reaction v_NDH in leaf = kNDH*(Fdf*(1 - PQH2f) - (1 - Fdf)*PQH2f/KNDH) for 2 Fdr + 1 PQ -> 2 Fdo + 1 PQH2 + 4 Hlt in umol/m^2/s # + 6 Hst ->
parameter kNDH = 4 in umol/m^2/s
parameter NDH_Km_PQ = 2 in umol/m^2
parameter NDH_Km_PQH2 = 2 in umol/m^2
parameter NDH_Km_Fdr = 0.5 in umol/m^2
parameter NDH_Km_Fdo = 0.5 in umol/m^2

### Eqn. 138
variable KNDH = exp((2*F*(Em_PQH2_7 - Em_Fd) - 4.0*F*pmf)/R/T)

### Eqn. 139
reaction v_FQR in leaf = FQRr/(FQRr + FQRo)*kFQR*Fdr*PQ/((FQR_Km_Fdr + Fdr)*(FQR_Km_PQ + PQ)) - FQRr/(FQRr + FQRo)*kFQR/KFQR*Fdo*PQH2/((FQR_Km_Fdo + Fdo)*(FQR_Km_PQH2 + PQH2)) for 2 Fdr + 1 PQ + 2 Hst -> 1 PQH2 + 2 Fdo in umol/m^2/s #  ->
parameter kFQR = 8 in umol/m^2/s
#parameter FQR_Km_PQ = 1 in umol/m^2 # 20 uM * 10 mL/m2 -> 0.2 umol/m2
#parameter FQR_Km_Fd = 0.5 in umol/m^2 # 1 uM * 10 mL/m2 -> 0.1 umol/m2
parameter FQR_Km_PQ = 2 in umol/m^2
parameter FQR_Km_PQH2 = 2 in umol/m^2
parameter FQR_Km_Fdr = 0.5 in umol/m^2
parameter FQR_Km_Fdo = 0.5 in umol/m^2

### Eqn. 140
variable KFQR = exp(2*F*(Em_PQH2_7 - Em_Fd)/(R*T))

### Eqn. 141
reaction vFQRr in stroma = kFQRr*(Thr*FQRo/stroma - Tho*FQRr/stroma/KFQRr) for 1 Thr + 1 FQRo -> 1 Tho + 1 FQRr in mM/s
parameter kFQRr = 100 in 1/mM/s

### Eqn. 142
variable KFQRr = exp(2*F*(Em_FQR - Em_Th)/(R*T))
variable Em_FQR = Em_FQR_7 - V_per_pH*(pHs - 7) in mV
parameter Em_FQR_7 = -276.5 in mV

### Eqn. 143 is generated by ODEDSL
species FQRr in leaf = 0 in umol/m^2
species FQRo in leaf = 0.2 in umol/m^2





### Eqn. 144
reaction vWWC in leaf = Fdr*kWWC*O2/(O2 + KwwcO2)/(Fdr + KwwcFd) for 1 Fdr + 1 Hst -> 1 Fdo in umol/m^2/s #
parameter kWWC = 5 in umol/m^2/s
parameter KwwcO2 = 7.9 in mmol/mol
parameter KwwcFd = 0.5 in umol/m^2



### Eqn. 145
reaction v_MDH in stroma = (MDH_Vm*OAAs*NADPH)/(MDH_Km_OAA*MDH_Km_NADPH*MDH_D) -
                           (MDH_Vm*MALs*NADP/(kMDH*one_molar/Hs))/(MDH_Km_MAL*MDH_Km_NADP*MDH_D) for 1 NADPH + 1 OAAs + 1 Hst -> 1 NADP + 1 MALs in mM/s #  ->
variable Hs = 10^(-pHs)*one_molar in M
parameter MDH_Km_NADPH = 39 in uM
parameter MDH_Km_NADP = 63 in uM
parameter MDH_Km_OAA = 48 in uM
parameter MDH_Km_MAL = 14.5 in mM
parameter kMDH = 2.78e12

### Eqn. 146
variable MDH_D = 1 + OAAs/MDH_Km_OAA + NADPH/MDH_Km_NADPH + MALs/MDH_Km_MAL + NADP/MDH_Km_NADP +
                     OAAs*NADPH/(MDH_Km_NADPH*MDH_Km_OAA) + MALs*NADP/(MDH_Km_NADP*MDH_Km_MAL)

### Eqn. 147
observed MDH_Vm = (MDHrf*MDH_Vmax + (1 - MDHrf)*MDH_Vmax/100)*NADPHf^nM/(NADPHf^nM + pN^nM) in mM/s
parameter MDH_Vmax = 1.2 in mM/s # 1.2 - 2.4 from Foyer 1993 and Fridlyand 1999
parameter nM = 8
parameter pN = 0.52

### Eqn. 148
reaction vMDHr in stroma = KthioMDH*(Thr*MDHo - Tho*MDHr/KThMDH) for 1 Thr + 1 MDHo -> 1 Tho + 1 MDHr in mM/s
parameter KthioMDH = 100 in 1/mM/s

### Eqn. 149
variable KThMDH = exp(2*F*(Em_MDH - Em_Th)/(R*T))
variable Em_MDH = Em_MDH_7 - V_per_pH*(pHs - 7) in mV
parameter Em_MDH_7 = -330 in mV

### Eqn. 150
reaction vMT in stroma = vMTmax*(MALsp*S2 -  MALcp*S1)/(S1 + S2 + 0.5*(S1*S4 + S2*S3)) for 1 MALs -> 1 OAAs in mM/s
parameter vMTmax = 2 in mM/s # Adjust in parallel to MDH

### Eqn. 151
variable S1 = vMTmax*(OAAsp + MALsp) in mM/s

### Eqn. 152
variable S2 = vMTmax*(OAAcp + MALcp) in mM/s

### Eqn. 153
variable S3 = OAAsp + MALsp

### Eqn. 154
variable S4 = OAAcp + MALcp

### Eqn. 155
variable OAAsp = OAAs/KmtOAA
parameter KmtOAA = 0.17 in mM

### Eqn. 156
variable MALsp = MALs/KmtMAL
parameter KmtMAL = 2.7 in mM

### Eqn. 157
variable OAAcp = OAAc/KmtOAA
parameter OAAc = 0.098 in mM

### Eqn. 158
variable MALcp = MALc/KmtMAL
parameter MALc = 1 in mM

### Eqn. 159 - 161 are generated by ODEDSL
species MALs in stroma = 3 in mM
species OAAs in stroma = 0.025 in mM
species MDHr in stroma = 0 in mM
species MDHo in stroma = 0.019 in mM


### Eqn. 162
#derivative d_SKi_dt of SKi = Vc*(1.0 - 0.5*phi)*fNRSK - SKi*kNRSKi in mM/s
#state SKi = 0 in mM
#parameter fNRSK = 0.035
#parameter kNRSKi = 0.01 in 1/s
#parameter Ka_SK = 0.5 in mM

### Eqn. 163
#derivative d_SK_dt of SK = SKi*kNRSKi - v_NR/stroma - SK_sink*SK/(SK + KmSK) in mM/s
#state SK = 0 in mM
#parameter KmSK = 5 in mM
#parameter SK_sink = 1 in mM/s

### Eqn. 164
#reaction v_NR in leaf = kNR*Fdr/(Fdr + NiR_Km_Fd)*SK/(SK + NiR_Km_SK)*ATP/(ATP + NiR_Km_ATP)*NO2/(NO2 + NiR_Km_NO2) for 8 Fdr + 1 ATP + 8 Hst -> 8 Fdo + 1 ADP in umol/m^2/s #  ->
reaction v_NR in leaf = kNR*Fdr/(Fdr + NiR_Km_Fd)*ATP/(ATP + NiR_Km_ATP)*NO2/(NO2 + NiR_Km_NO2) for 8 Fdr + 1 ATP + 8 Hst -> 8 Fdo + 1 ADP in umol/m^2/s #
parameter kNR = 3 in umol/m^2/s
parameter NiR_Km_Fd = 0.12 in umol/m^2
parameter NiR_Km_ATP = 10 in uM
#parameter NiR_Km_SK = 100 in uM
parameter NiR_Km_NO2 = 23.7 in uM
parameter NO2 = 25 in uM # Around the Km...










### Eqn. 165
observed Vc =  fRuBP*fRB*kc*RB*Cc/(Cc + Kmc*(1.0 + O2/Kmo)) in mM/s
parameter kc = 4.4 in 1/s
parameter Kmc = 309 in umol/mol
parameter Kmo = 179 in mmol/mol
parameter RB = 1.45 in mM # 1.6. Adjust to new volume
parameter O2 = 210 in mmol/mol

### Eqn. 166
variable Vo =  fRuBP*fRB*ko*RB*O2/(O2 + Kmo*(1.0 + Cc/Kmc)) in mM/s
parameter ko = 1.0 in 1/s

### Eqn. 167
observed phi = ifelse(Vc > zeromMs, Vo/Vc, 0)
constant zeromMs = 0 in mM/s

### Eqn. 168
observed fRuBP = 1/(2*RB)*(RB + Kmapp_RuBP + RuBP - sqrt((RB + Kmapp_RuBP + RuBP)^2 - 4*RB*RuBP))


### Eqn. 169
variable Kmapp_RuBP = KmRuBP*(1 + PGA/KiPGA) in mM
parameter KmRuBP = 0.02 in mM
parameter KiPGA = 0.84 in mM


### Eqn. 170
#variable fRBss = fRBmin + (fRBalpha*PAR + (fRBmax - fRBmin) - sqrt( (fRBalpha*PAR + (fRBmax - fRBmin))^2.0 - 4*fRBtheta*fRBalpha*PAR*(fRBmax - fRBmin) ))/(2*fRBtheta)
observed fRBss = ifelse(PAR > zeroumolm2s, fRCA*RCA/(fRCA*RCA + KaRCA), fRBdk)#*min(1.0, ac + bc*Cc)
parameter KaRCA = 0.1 in mM
#parameter ac = 0.27
#parameter bc = 0.014 in mol/umol
observed fRCA = fRCAm + (1 - fRCAm)*(fRCAr*exp(-fRCAsr*ADP/ATP) + fRCAo*exp(-fRCAso*ADP/ATP)) #max(0, 1 - (ADP/ATP)/(ADP_ATP_max))
parameter fRCAsr = 2.004
parameter fRCAso = 10.8
parameter fRCAm = 0.042
parameter fRBdk = 0.2 # 0.4
variable fRCAr = RCAr/RCA
variable fRCAo = RCAo/RCA
#parameter fRBmax = 0.95
#parameter fRBalpha = 0.002 in m^2*s/umol
#parameter fRBtheta = 0.8

reaction vRCAr in stroma = kRCAr*(Thr*RCAo - Tho*RCAr/KRCA) for 1 Thr + 1 RCAo -> 1 Tho + 1 RCAr in mM/s
variable RCA = RCAr + RCAo in mM
parameter kRCAr = 100 in 1/mM/s
species RCAo in stroma = 1.23 in mM
species RCAr in stroma = 0 in mM
variable KRCA = exp(2*F*(Em_RCA - Em_Th)/(R*T))
parameter Em_RCA_7 = -290 in mV
variable Em_RCA = Em_RCA_7 - V_per_pH*(pHs - 7) in mV


### Eqn. 171
derivative d_fRB_dt of fRB = ifelse(fRBss > fRB, (fRBss - fRB)*kRBi*RCA*fRCA, (fRBss - fRB)*kRBd) in 1/s
parameter kRBi = 0.0045 in 1/mM/s # 0.00677 . 6.8e-3 in 1/s # more reasonable value for Arabidopsis
parameter kRBd = 4.2e-4 in 1/s
state fRB = 0.4

### Eqn. 172
variable Vr = fR*Vrmax*PGA/(PGA + KmPGA)*NADPH/(NADPH + KmNADPH)*ATP/(ATP + KmATP) in mM/s
parameter Vrmax = 12 in mM/s # 10. Adjust new volume
parameter KmPGA = 3.5 in mM
parameter KmNADPH = 0.023 in mM
parameter KmATP = 0.24 in mM


### Eqn. 173
variable fRss = fRmin + (fRalpha*PAR + (1 - fRmin) - sqrt( (fRalpha*PAR + (1 - fRmin))^2.0 - 4*fRtheta*fRalpha*PAR*(1 - fRmin) ))/(2*fRtheta)
parameter fRmin = 0.04
parameter fRalpha = 0.00248 in m^2*s/umol
parameter fRtheta = 0.96

### Eqn. 174
derivative d_fR_dt of fR = ifelse(fRss > fR, (fRss - fR)*fR_k_i, (fRss - fR)*fR_k_d) in 1/s
parameter fR_k_i = 6.3e-3 in 1/s
parameter fR_k_d = 7.5e-3 in 1/s
state fR = 0

### Eqn. 175
derivative d_PGA_dt of PGA = 2.0*Vc + 1.5*Vc*phi - Vr in mM/s
state PGA = 19 in mM

### Eqn. 176
derivative d_RuBP_dt of RuBP = (1.0 + phi)/(2.0 + 1.5*phi)*Vr - Vc*(1.0 + phi) in mM/s
state RuBP = 1 in mM

### Eqn. 177 (part of the equation is generated by ODEDSL)
derivative d_ATP_dt of ATP = -(3 + 3.5*phi)*Vr/(2 + 1.5*phi)  in mM/s
species ATP in stroma = 0.5 in mM

### Eqn. 178 (part of the equation is generated by ODEDSL)
derivative d_ADP_dt of ADP = (3 + 3.5*phi)*Vr/(2 + 1.5*phi)  in mM/s
species ADP in stroma = 0.5 in mM

### Eqn. 179
observed Pi = PIt - 3*ATP - 2*ADP - 2.0*RuBP - PGA in mM
parameter PIt = 30 in mM

### Eqn. 180 (part of the equation is generated by ODEDSL)
derivative d_NADPH_dt of NADPH = -(2.0 + 2.0*phi)*Vr/(2 + 1.5*phi) in mM/s
species NADPH in stroma = 1e-4 in mM

### Eqn. 181 (part of the equation is generated by ODEDSL)
derivative d_NADP_dt of NADP = (2.0 + 2.0*phi)*Vr/(2 + 1.5*phi)  in mM/s
species NADP in stroma = 0.9 in mM

### Eqn. 182
#derivative d_Cc_dt of Cc = ((Ca - Cc)*1.0/(1.0/gm + 1.0/gs) - An)*molar_volume/Vol_ref in umol/mol/s
parameter Vol_ref = 0.5 in L/m^2
#parameter gm = 0.2 in mol/m^2/s
#variable molar_volume = R*T/P in L/mol
#state Cc = 400 in umol/mol


# CO2 diffusion
derivative d_Ccyt_dt of Ccyt = ((Ci - Ccyt)*gw + Rd + 0.5*PR*kPR*stroma - (Ccyt - Cc)*gc)*Mv/Vol_ref in umol/mol/s
derivative d_Cc_dt of Cc = ((Ccyt - Cc)*gc - Vc*stroma)*Mv/Vol_ref in umol/mol/s
derivative d_Ci_dt of Ci = ((Ca - Ci)*gs - (Ci - Ccyt)*gw)*Mv/Vol_ref in umol/mol/s
state Cc = 380 in umol/mol
state Ccyt = 380 in umol/mol
state Ci = 380 in umol/mol
observed An = (Ca - Ci)*gs in umol/m^2/s
parameter Rd = 0.5 in umol/m^2/s # 1
parameter gc = 0.28 in mol/m^2/s
parameter gw = 0.75 in mol/m^2/s
variable Mv = R*T/P in L/mol


derivative d_PR_dt of PR = Vc*phi - PR*kPR in mM/s
state PR = 0 in mM
parameter kPR = 0.01 in 1/s


### Eqn. 183
derivative d_gs_dt of gs = gs_k*(log((gss - gs_r0)/(gs - gs_r0)))*(gs - gs_r0) in mol/m^2/s^2
parameter gs_k = 7e-4 in 1/s # 8.53e-4
parameter gs_r0 = 0.01 in mol/m^2/s
state gs = 0.10 in mol/m^2/s

### Eqn. 184
observed gss = fI*gsm in mol/m^2/s

# Relative effect of irradiance on stomatal conductance
variable fI_a = thetafI
variable fI_b = -(1 + fI0 + alphafI*PAR)
variable fI_c = fI0 + alphafI*PAR
variable fI = (-fI_b - sqrt(fI_b^2 - 4*fI_a*fI_c))/(2*fI_a)
parameter fI0 = 0.36 # 3.116e-1
parameter gsm = 0.16 in mol/m^2/s # 0.1486272
parameter alphafI = 5.6e-3 in 1/umol*m^2*s # 5.05e-3
parameter thetafI = 0.97427


### Eqn. 185
#observed An = (Vc - 0.5*PR*kPR)*stroma - Rd in umol/m^2/s
#parameter Rd = 1 in umol/m^2/s

# NOT DOCUMENTED -> Makes it easier reproduce metabolism in darkness (we could set the dark-adapted initial values "manually")
# It still does not work that well...
reaction ATPex in stroma = ifelse(PAR > zeroumolm2s, 0.0, 1.0)*K_ATPex*ATP/(ATP + KmATPex) for 1 ATP -> 1 ADP in mM/s # 1 PIs
constant zeroumolm2s = 0 in umol/m^2/s
parameter KmATPex = 0.24 in mM
parameter K_ATPex = 0.1 in mM/s # 0




# Observers that are not documented in the description of the model
observed P680fex =  PSII{P680[e]}/PSII
observed antennaex =  PSII{Antenna[1]}/PSII
observed P680fp =  PSII{P680[p]}/PSII
observed QarX =  PSII{Qa[1]}/PSII
observed PQH2b = PSII{Qb[2]}/PSII
observed PCf = PCr/(PCr + PCo)
observed PQH2p = cyt{Qp[2]}/cyt
observed P700ox = PSI{P700[p]}/PSI
observed NADPHf = NADPH/(NADP + NADPH)
observed Thrf  = Thr/(Thr + Tho)
observed Fdf  = Fdr/(Fdr + Fdo)
observed ATPaserf = ATPaser/(ATPaser + ATPaseo)
observed MDHrf = MDHr/(MDHr + MDHo)

#observed frec = (vcra)/(LEFII + vcra)
observed PhiPSII = LEFII/(PAR*alphap*sigma2)
observed PhiPSI = LEFcyt/(PAR*alphap*(1 - sigma2))
#observed FvpFmp = (Fmp - Fop)/Fmp
#observed FqpFmp = (Fmp - Fp)/Fmp
#observed FvppFmpp = (Fmpp - Fop)/Fmpp

#observed Fmp = alphar*(kf2/(kf2 + Q*kE + kD2)*(1 - PSIId) + kf2/(kf2 + kDinh + kD2)*PSIId)
observed Fmp = alphar*kf2/(kf2 + Q*kE + kD2)
#observed Fmpp = kf2/(kf2 + kD2)*(1 - PSIId) + kf2/(kf2 + kDinh + kD2)*PSIId
observed Fmpp = kf2/(kf2 + kD2)
observed Fm = kf2/(kf2 + kD2)
observed Fo = kf2/(kf2 + kD2 + kcso)
#observed Fop = alphar*(kf2/(kf2 + Q*kE + kD2 + kcso)*(1 - PSIId) + kf2/(kf2 + kDinh + kD2)*PSIId)
observed Fop = alphar*kf2/(kf2 + Q*kE + kD2 + kcso)
#observed Fopp = kf2/(kf2 + kD2 + kcso)*(1 - PSIId) + kf2/(kf2 + kDinh + kD2)*PSIId
observed Fopp = kf2/(kf2 + kD2 + kcso)
#observed Fp = alphar*(kf2/(kf2 + Q*kE + kD2)*(1 - PSIId)*QarX + kf2/(kf2 + Q*kE + kD2 + kcso)*(1 - PSIId)*(1 - QarX) + kf2/(kf2 + kDinh + kD2)*PSIId)
observed Fp = alphar*(kf2/(kf2 + Q*kE + kD2)*QarX + kf2/(kf2 + Q*kE + kD2 + kcso)*(1 - QarX))

#observed Hflux = -vFFT*HPR  in umol/m^2/s
observed vATP  = (3 + 3.5*phi)*Vr/(2 + 1.5*phi)*stroma in umol/m^2/s # in umol/m^2/s
observed vNADPH = (2.0 + 2.0*phi)*Vr/(2 + 1.5*phi)*stroma in umol/m^2/s
observed NDH_Delta_Gp = -(2*F*(Em_PQH2_7 - Em_Fd) - 4.0*F*pmf) - R*T*log(max(Fdf*(1 - PQH2f),0)/max((1 - Fdf)*PQH2f, 0)) in J/mol # This is what we see in Strand et al. (2015)
observed vNDH = v_NDH*2.0 in umol/m^2/s
observed vFQR = v_FQR*2.0 in umol/m^2/s
observed WWC = vWWC in umol/m^2/s
observed vMDH = v_MDH*stroma*2 in umol/m^2/s
observed vNiR = 8*v_NR in umol/m^2/s


################################################################################
################### Compartments, Constants & Forcings  ########################
################################################################################

# Compartments
compartment leaf is const = 1
compartment lumen is const = 1.30 in cm^3/m^2 # 2*Ct = 2e3*452e-6 = 0.9 mL/m2 # ALTERNATIVE: Vst/Vlu = 8 -> 1.25
compartment stroma is const = 10.44 in cm^3/m^2 # Vstroma/Ct = 8 => 7.2 mL/m2 # ALTERNATIVE: 25 uL/mg chl -> 10.05
compartment thylakoid is const = 251 # 2.5e5 m2/mol chl -> 113. # ALTERNATIVE: 0.8 nL/cm2 -> 156

# Forcings
forcing PAR = [1000,1000.] at [0,1.] in umol/m^2/s
forcing Ca = [400,400.] at [0,1.] in umol/mol

# Physical constants
constant R = 8.31 in J/mol/K
constant T = 298.15 in K
constant F = 96485.0 in s*A/mol
constant P = 101 in kPa
constant one_molar = 1 in M
